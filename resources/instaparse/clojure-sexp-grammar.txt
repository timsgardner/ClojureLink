top = tok-seq-H

<tok-seq-H> = (whitespace? tok-H)* whitespace?

<tok-H> = comment | parenthetical-H | string | word | escaped | regex | clr-type

<parenthetical-H> = list | vector | map | set | function-literal

list =  <'('> tok-seq-H <')'>

vector = <'['> tok-seq-H <']'>

map = <'{'> tok-seq-H <'}'>

set = <'#{'> tok-seq-H <'}'>

function-literal = <'#('> tok-seq-H <')'>

# this abomination is strictly here because of that stupid alternation bug in java regexes

string = "\"" (#"\\\""? #"((?!\\\")[^\"])*")* #"\\\""? "\""

clr-type =  "|" (#"\|\|"? #"[^|]*")* #"\|\|"? "|"

regex = '#' string

comment = #";.*"

escaped = #'\\(\W|\w+)'

word = #"(?!#[\s\(\)\{\}\[\]\"])(@|'|#['_]|~@?|\^|[^;\s\~\(\)\{\}\[\]\"\\,]+)"

whitespace = #'[\s,]+'
