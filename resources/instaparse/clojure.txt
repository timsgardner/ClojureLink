# Eliminate ambiguity by adding recursive negative lookahead at end of RHS

# this breaks for strings nested beyond level 1 or so, fix when you understand parsing better

# top -----------------------------------------

top = tokseq-H

# expression-H ----------------------------------

<expression-H> =  atom-H / known-literal-H / tagged-literal

<known-literal-H> = list | vector | map | set | quote-literal | character-literal | deref-literal | metadata-literal | syntax-quote-literal | function-literal | unquote-literal | unquote-splicing-literal | ignore-form-literal | var-quote-literal | argument-literal | ampersand-attack | regex-literal | access-literal | typehint | weird-meta | constructor

list = <'('> tok-H* <')'>
vector = <'['> tok-H* <']'>
map = <'{'> tok-H* <'}'>

set = <"#{"> tok-H* <"}">

weird-meta = <"#^"> expression-H

quote-literal = <"'"> expression-H

character-literal = #"\\."

deref-literal = #"(?<![~_])@" expression-H

metadata-literal = <"^"> (keyword | map)

syntax-quote-literal = <"`"> expression-H

function-literal = <"#("> tokseq-H <")">

unquote-literal = <"~"> !"@" expression-H

unquote-splicing-literal = <"~@"> expression-H

ignore-form-literal = <"#_"> expression-H

var-quote-literal = <"#'"> name

tagged-literal = <"#"> name (list | vector | map | (whitespace expression-H))

<atom-H> = (number | string | keyword | name)

number = #'e?[0-9]+(\.[0-9]+)?'

name = #"[a-zA-Z*+<>=!\-/_?]([\S&&[^.\(\)\[\]\{\}\"\@\#\\]]*#?)" | clr-type-H
keyword = #':(:?)' name

argument-literal = <"%"> (#'[0-9]'|'&')? !atom-H

ampersand-attack = <#"(?<!%)&"> tokseq-H !tok-H

regex-literal = <'#'> string

<access-literal> = dot-access | field-access

dot-access = <"."> !"-" name

field-access = <".-"> name

typehint = <"^"> name

constructor = name "."

# string --------------------------------------

string = #"\"(\\\"|(?s).)*?\""

# clr type ---------------------------------

clr-type = #"\|(\|\||(?s).)*?\|"
<clr-type-H> = clr-type

# comments ------------------------------------

comment = #"(?m)(;.*?$)"

# breaks --------------------------------------

whitespace = whitespace-H
<whitespace-H> = (nbsp-H | linebreak)+ !whitespace-H

nbsp = nbsp-H
<nbsp-H> = (comma | space | tab)+ !nbsp-H

linebreak = #"\n|\r"
comma = ","
space = " "
tab = #"\t"

# utilities ----------------------------------

<tokseq-H> = tok-H+
<tok-H> = whitespace / comment / expression-H
